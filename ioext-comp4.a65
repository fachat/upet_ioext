
.(

FN_PTR          =$da
FN_LEN          =$d1

	; -------------------------------------------------------
	; the following code is copied into the ROM and called
	; from the patches into the kernal I/O code

;--------------------------------------------------
; this is injected at $f2a6
; the original only just sends UNTALK/UNLISTEN for IEEE
;
; F2A6	iF2A6	LDA $B0		; Default Output (CMD) Device (3)
; F2A8		CMP #$04
; F2AA		BCC $F2AF
; F2AC		JSR $F1B9	; untlk	Send UNLISTEN
; F2AF	iF2AF	LDA $AF		; Default Input Device (0)
; F2B1		CMP #$04
; F2B3		BCC $F2B8
; F2B5		JSR $F1AE	; untlk	Send UNTALK

&CLRCH 	.(
	phx
	php
	sei
	lda $b0
	jsr is_ioext
	bcs out_ioext
	cmp #$04
	bcc chkinput
	jsr $f1b9		; unlisten
chkinput
	lda $af
	jsr is_ioext
	bcs in_ioext
	plp
	plx
	jmp $f2b3
clrch_exit
	plp
	plx
	jmp $f2b8
out_ioext
	ldx #IOEXT_BANK
	stx BANKREG
	jsr IO_CLRCH_OUT
	ldx #DEFAULT_BANK
	stx BANKREG
	beq chkinput
in_ioext
	ldx #IOEXT_BANK
	stx BANKREG
	jsr IO_CLRCH_IN
	ldx #DEFAULT_BANK
	stx BANKREG
	beq clrch_exit
	.)



;--------------------------------------------------
; this is injected at $f818 where keyboard is already branched out
;
; F810		LDA $D4		; Current Device Number
; F812		BNE $F818
; ...
; F818	iF818	CMP #$03
; F81A		BEQ $F828
; F81C		BCS $F82D
;
&CKOUT	.(
	jsr is_ioext
	bcs chkout_ioext
	cmp #3
	beq chkout_screen
	jmp $f81c
chkout_screen
	jmp $f828	
chkout_ioext
	php
	sei
	ldx #IOEXT_BANK
	stx BANKREG
	jsr IO_CHKOUT
	ldx #DEFAULT_BANK
	stx BANKREG
	plp
	lda $d4
	jmp $f828
	.)

;--------------------------------------------------
; this is injected a $f7ca
;
; F7C6		LDA $D4		; Current Device Number
; F7C8		BEQ $F7DA
; F7CA		CMP #$03
; F7CC		BEQ $F7DA
; F7CE		BCS $F7DF
;
&CHKIN	.(
	jsr is_ioext
	bcs chkin_ioext
	cmp #3
	beq chkin_screen
	jmp $f7ce
chkin_ioext
	php
	sei
	ldx #IOEXT_BANK
	stx BANKREG
	jsr IO_CHKIN
	ldx #DEFAULT_BANK
	stx BANKREG
	plp
	lda $d4
chkin_screen
	jmp $f7da
	.)

;--------------------------------------------------
; Injected at $f266
;
; F266	iF266	PHA
; F267		LDA $B0		; Default Output (CMD) Device (3)
; F269		CMP #$03
; F26B		BNE $F271
; F26D		PLA
; F26E		JMP $E202	; -	Output to Screen
; F271	iF271	BMI $F277
; F273		PLA
; F274		JMP $F19E
; F277	iF277	PLA

&CHROUT	.(
	pha
	lda $b0
	jsr is_ioext
	bcs out_ioext
	jmp $f269
out_ioext
	phx
	phy
	tay		; translated device number
	tsx
	lda $0103,x	; AC output value
	php
	sei
	ldx #IOEXT_BANK
	stx BANKREG
	jsr IO_OUT
	ldx #DEFAULT_BANK
	stx BANKREG
	plp
	ply
	plx
	pla
	rts
	.)

;--------------------------------------------------
; Injected at $f224
; 
; reason is that GET# jumps there from F205 if
; current input is not coming from screen or keyboard
;
; F215	iF215	LDA $AF		; Default Input Device (0)
; F217		BNE $F224
; F219		LDA $C6		; Cursor Column on Current Line
; F21B		STA $A4
; F21D		LDA $D8		; Current Cursor Physical Line Number
; F21F		STA $A3		; Cursor Y-X Pos. at Start of INPUT
; F221		JMP $E116	; Input From Screen or Keyboard
; F224	iF224	CMP #$03
; F226		BNE $F231	; -	Get From Tape / IEEE
; F228		STA $AC		; Flag: INPUT or GET from keybord
&CHRIN	.(
	jsr is_ioext
	bcs out_ioext
	cmp #3
	bne notscr
	jmp $f228
notscr	jmp $f231
out_ioext
	phx
	phy
	php
	sei
	ldx #IOEXT_BANK
	stx BANKREG
	jsr IO_IN
	ldx #DEFAULT_BANK
	stx BANKREG
	bcs nobyt
	; needs to clr Z flag
	plp
	ply
	plx
	bne zclr
	cpx nobyt	; compare with no-zero mem
zclr	rts
nobyt	; needs to set Z flag
	plp
	ply
	plx
	lda zero
	rts
	.)

;--------------------------------------------------
; this is injected at F35D
;
; F356	iF356	LDA $D4		; Current Device Number
; F358		BNE $F35D
; F35A	iF35A	JMP $BF00	; synerr	Output ?SYNTAX Error
; F35D	iF35D	CMP #$03
; F35F		BEQ $F35A
; F361		BCC $F3D4
; F363		LDA #$60
&LOAD	.(
	jsr is_ioext
	bcs load_ioext
	cmp #3
	beq load_err
	jmp $f361
load_ioext
load_err
	jmp $f35a		; error - load not supported
	.)

;--------------------------------------------------
; this is injected at $f6ec
;
; F6E3	iF6E3	LDA $D4		; Current Device Number
; F6E5		BNE $F6EC
; F6E7	iF6E7	LDY #$74
; F6E9		JMP $F5AF
; F6EC	iF6EC	CMP #$03
; F6EE		BEQ $F6E7
; F6F0		BCC $F742
&SAVE	.(
	jsr is_ioext
	bcs save_ioext
	cmp #3
	beq save_err
	jmp $f6f0
save_ioext
save_err
	jmp $f6e7		; error, save not supported
	.)


;--------------------------------------------------
; close is injected at f2f4, where keyboard and
; screen have already branched out
;
; F2EC	LDA $D4
; F2EE	BEQ $F318
; F2F0	CMP #$03
; F2F2	BEQ $F318
; F2F4	BCS $F315
; F2F6	LDA $D3		; tape
; F2F8	AND #$0f
&CLOSE	.(
	jsr is_ioext
	bcs close_ioext
	cmp #3
	bcs closenottape
	lda $D3
	jmp $f2f8
closenottape
	jmp $F315
close_ioext
	php
	sei
	ldx #IOEXT_BANK
	stx BANKREG
	jsr IO_CLOSE
	ldx #DEFAULT_BANK
	stx BANKREG
	plp
	jmp $F318
	.)

;--------------------------------------------------
; open is injected at f593
; where 0 is already escaped to an RTS (keyboard),
; and screen (3) is already branched out as well
; the device number is in AC
;
; F58F	CMP #$03
; F591	BEQ $F5E4	; screen
; F593	BCC $F598	; 1,2 = TAPE
; F595	JMP $F4A5	; IEEE
&OPEN	.(
	jsr is_ioext
	bcs open_ioext
	cmp #3
	bcc opentape
	jmp $f4a5
opentape
	jmp $f598
open_ioext
	; copy over filename to IOEXT_BANK
	; so that only need to give the length in registers
	; and have no bank stuff in the actual core
	pha
	.(
&&zero	=*+1		; for CHRIN
	ldy #0
	ldx #0
l	cpy FN_LEN
	beq e
	lda (FN_PTR),y
	sta IOEXT_FILENAME+(IOEXT_BANK<<15),x
	inx
	iny
	bne l
e	lda #0
	sta IOEXT_FILENAME+(IOEXT_BANK<<15),x
	.)
	pla
	php
	sei
	ldx #IOEXT_BANK
	stx BANKREG
	jsr IO_OPEN
	ldx #DEFAULT_BANK
	stx BANKREG
	plp
	rts
	.)

;--------------------------------------------------

&NEWIRQ	.(
	lda #IOEXT_BANK
	sta BANKREG
	jsr IO_IRQ
	lda #DEFAULT_BANK
	sta BANKREG
&&origirq	=*+1
	jmp $ffff
	.)

&NEWRESET .(
	lda #IOEXT_BANK
	sta BANKREG
	jsr IO_RESET
	lda #DEFAULT_BANK
	sta BANKREG
&&origres	=*+1
	jmp $ffff
	.)

;--------------------------------------------------
; filter device number for IOEXT devices
;
; Input:
;	2	Serial1 - replaces Tape2
;	24	(I2C)
;	25	Tape2 (mapped to 2 on original)
;	26	Serial2
;	27	(SPI)
; Output:
;	C=1 ->	is IOEXT, AC is
;	0	(I2C)
;	1	Ser2
;	2	Ser1
;	3	(SPI)
;	C=0 -> original,
is_ioext .(
	cmp #2
	bcc no		; tape1
	beq ser1	; ser1
	cmp #16		; rest up to 15 is IEEE/IEC
	bcc no
	cmp #20		; 4 device nos in ioext
	bcs no
	cmp #17		;
	beq tape2
	.byt $2c
ser1	lda #17
yes	and #3		; return AC as 0,1,2,3
	rts
tape2	lda #2
no	clc
	rts
	.)



.)

