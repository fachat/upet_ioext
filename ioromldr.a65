
/*
 * this file installs the iorom driver in bank 7 and patches the BASIC 4 ROM
 * to include the ROM companion functions.
 */

IOEXT_BASE	=$7000
IOCOMPADDR	=$d4c0
IOCOMPBANK	=14
BANK		=$e802


	.word $0401
	*=$0401

	.word eol		; BASIC link pointer
	.word 10		; line number
	.byt $9e, "1040", 0	; BASIC code (tokenized)
eol	.word 0			; BASIC link pointer, 0 means end of code
	.dsb 1040-*

	; start after sys
	sei

	; native mode
	clc
	xce
	; make ROM writeable
	lda #0
	sta $e801

	; copy end of monitor to monitor entry
	.(
	ldy #0
l1	lda $d66b,y
	sta $d4ac,y
	iny
	cpy #7
	bcc l1
	.)

	; clear out monitor area
	; $d4b3 - d7a3
	.(
	lda #$ea	; NOP opcode
	ldy #0
l1	sta $d4b3,y	; monitor code
	sta $d5b3,y
	sta $d7a3-$0100,y
	iny
	bne l1
	.)

	; 16bit index registers
	rep #%00010000
	.xl

	; copy over core
	.(
	ldx #end-dosrom
l1	lda dosrom-1,x
	sta IOEXT_BASE + (IOCOMPBANK<<15) -1,x
	dex
	bne l1
	.)

	; copy over companion and
	; install it
	.(
	ldx #dosrom - MOVE
l1	lda MOVE-1,x
	sta IOCOMPADDR-1,x
	dex
	bne l1
	.)

	lda #$4c
	ldx #CLRCH

	sta $f2a6
	stx $f2a7

	ldx #CKOUT
	sta $f818
	stx $f819

	ldx #CHKIN
	sta $f7ca
	stx $f7cb

	ldx #CHROUT
	sta $f266
	stx $f267

	ldx #CHRIN
	sta $f215
	stx $f216

	ldx #LOAD
	sta $f35d
	stx $f35e

	ldx #SAVE
	sta $f6ec
	stx $f6ed

	ldx #CLOSE
	sta $f2f4
	stx $f2f5

	ldx #OPEN
	sta $f593
	stx $f594

	; 8bit index registers
	sep #%00010000
	.xs

	; install interrupt code
	.(
        lda #<NEWIRQ
        ldx #>NEWIRQ

        ; set interrupt addr from a/x
        php
        sei

	; make ac free
        pha

        ; x is not modified
        lda $e455       ; standard interrupt routine
        cmp #$4c        ; JMP on some ROMs
        beq dojmp
        lda #<$e456
        ldy #>$e456
        bne inject

dojmp   ldy $e457       ; E455 JMP XXXX ; get address from JMP
        lda $e456
        inc             ; no overflow in current ROM
inject
        sta addr2
        sty addr2+1
        sta addr3
        sty addr3+1

        pla
	; a/x has the new interrupt target address
        clc
        .byt $24
next2   sec
addr3   =*+1
        ldy $ffff
addr2   =*+1
        sta $ffff       ; set from above
        inc addr2
        inc addr3
        ; no overflow on current ROMs
        bcs exit
        phy
        txa
        plx
        bcc next2
exit	; x/y has the original interrupt routine address
	; we're patching into.
	
        ; save the original addr
        stx origaddr
        sty origaddr+1

        plp
	.)

	; emulation mode
	sec
	xce

	; restore memory protection
	lda #$d0
	sta $e801

	cli

	ldy #0
tl	lda text,y
	beq tend
	jsr $ffd2
	iny
	bne tl
tend	rts
text
	.asc 13,"INSTALLATION SUCCESSFUL",13, 0	

STATUS	=$96

MOVE:
	*=IOCOMPADDR

#include "ioext-comp.a65"

	*=*-IOCOMPADDR+MOVE
dosrom:
	*=IOEXT_BASE
#define	PET
#include "ioext-core.a65"
	*=*-IOEXT_BASE+dosrom
end

