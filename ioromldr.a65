
/*
 * this file installs the iorom driver in bank 7 and patches the BASIC 4 ROM
 * to include the ROM companion functions.
 */

IOEXT_BASE	=$7000
IOEXT_COMP	=$d4c0
IOEXT_BANK	=14
DEFAULT_BANK	=0
BANKREG		=$e802
IOEXT_FILENAME	=$0200
FN_PTR		=$da
FN_LEN		=$d1


	.word $0401
	*=$0401

	.word eol		; BASIC link pointer
	.word 10		; line number
	.byt $9e, "1040", 0	; BASIC code (tokenized)
eol	.word 0			; BASIC link pointer, 0 means end of code
	.dsb 1040-*

	; start after sys
	sei

	; native mode
	clc
	xce
	; make ROM writeable
	lda #0
	sta $e801

	; -----------------------------
	; remove monitor to make room 

	; copy monitor exit code to monitor entry
	.(
	ldy #0
l1	lda $d66b,y
	sta $d4ac,y
	iny
	cpy #7
	bcc l1
	.)

	; clear out monitor area
	; $d4b3 - d7a3
	.(
	lda #$ea	; NOP opcode
	ldy #0
l1	sta $d4b3,y	; monitor code
	sta $d5b3,y
	sta $d7a3-$0100,y
	iny
	bne l1
	.)

	; 16bit index registers
	rep #%00010000
	.xl

	; -----------------------------
	; copy over core
	.(
	ldx #end-dosrom
l1	lda dosrom-1,x
	sta IOEXT_BASE + (IOEXT_BANK<<15) -1,x
	dex
	bne l1
	.)

	; -----------------------------
	; copy over companion

	.(
	ldx #dosrom - MOVE
l1	lda MOVE-1,x
	sta IOEXT_COMP-1,x
	dex
	bne l1
	.)

	; -----------------------------
	; patch it into ROM
	.(
	lda #$4c
	ldx #CLRCH

	sta $f2a6
	stx $f2a7

	ldx #CKOUT
	sta $f818
	stx $f819

	ldx #CHKIN
	sta $f7ca
	stx $f7cb

	ldx #CHROUT
	sta $f266
	stx $f267

	ldx #CHRIN
	sta $f224
	stx $f225

	ldx #LOAD
	sta $f35d
	stx $f35e

	ldx #SAVE
	sta $f6ec
	stx $f6ed

	ldx #CLOSE
	sta $f2f4
	stx $f2f5

	ldx #OPEN
	sta $f593
	stx $f594
	.)

	; -----------------------------
	; init reset code

	.(
	ldx #NEWRESET
	ldy $fd1c
	stx $fd1c
	sty origres
	.)
	
	; -----------------------------
	; 8bit index registers
	sep #%00010000
	.xs

	; -----------------------------
	; install interrupt code
	.(
        lda #<NEWIRQ
        ldx #>NEWIRQ

        ; set interrupt addr from a/x
        php
        sei

	; make ac free
        pha

        ; x is not modified
        lda $e455       ; standard interrupt routine
        cmp #$4c        ; JMP on some ROMs
        beq dojmp
        lda #<$e456
        ldy #>$e456
        bne inject

dojmp   ldy $e457       ; E455 JMP XXXX ; get address from JMP
        lda $e456
        inc             ; no overflow in current ROM
inject
        sta addr2
        sty addr2+1
        sta addr3
        sty addr3+1

        pla
	; a/x has the new interrupt target address
        clc
        .byt $24
next2   sec
addr3   =*+1
        ldy $ffff
addr2   =*+1
        sta $ffff       ; set from above
        inc addr2
        inc addr3
        ; no overflow on current ROMs
        bcs exit
        phy
        txa
        plx
        bcc next2
exit	; x/y has the original interrupt routine address
	; we're patching into.
	
        ; save the original addr
        stx origirq
        sty origirq+1

	; -----------------------------
	; exit install

        plp
	.)

	; emulation mode
	sec
	xce

	; init the serial code
	lda #$60	; RTS
	sta origres-1
	jsr NEWRESET
	lda #$4c	; JMP
	sta origres-1

	; restore memory protection
	lda #$d0
	sta $e801

	cli

	ldy #0
tl	lda text,y
	beq tend
	jsr $ffd2
	iny
	bne tl
tend	rts
text
	.asc 13,"INSTALLATION SUCCESSFUL",13, 0	

STATUS	=$96

MOVE:
	*=IOEXT_COMP

#include "ioext-comp.a65"

	*=*-IOEXT_COMP+MOVE
dosrom:
	*=IOEXT_BASE
#define	PET
#include "ioext-core.a65"
	*=*-IOEXT_BASE+dosrom
end

