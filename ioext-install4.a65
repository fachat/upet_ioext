
/*
 * this file installs the iorom driver in bank 7 and patches the BASIC 4 ROM
 * to include the ROM companion functions.
 */

	.xl

ioext_install4_xl .(
        ; -----------------------------           
        ; remove monitor to make room             
                                               
        ; copy monitor exit code to monitor entry 
        .(                                        
        ldy #0                                    
l1      lda $d66b,y                               
        sta $d4ac,y                               
        iny                                       
        cpy #7                                    
        bcc l1                                    
        .)                                        

        ; clear out monitor area              
        ; $d4b3 - d7a3                        
        .(                                    
        lda #$ea        ; NOP opcode          
        ldy #0                                
l1      sta $d4b3,y     ; monitor code        
        iny
	cpy #$d7a3-$d4b3
        bcc l1                                
        .)                                    

        ; -----------------------------             
        ; copy over companion                       
                                                    
        .(                                          
        ldx #dosrom - MOVE                          
l1      lda MOVE-1,x                                
        sta IOEXT_COMP-1,x                          
        dex                                         
        bne l1                                      
        .)                                          

	rts
	.)


ioext_patch4_xl .(

	; -----------------------------
	; patch it into ROM
	.(
	lda #$4c
	ldx #CLRCH

	sta $f2a6
	stx $f2a7

	ldx #CKOUT
	sta $f818
	stx $f819

	ldx #CHKIN
	sta $f7ca
	stx $f7cb

	ldx #CHROUT
	sta $f266
	stx $f267

	ldx #CHRIN
	sta $f224
	stx $f225

	ldx #LOAD
	sta $f35d
	stx $f35e

	ldx #SAVE
	sta $f6ec
	stx $f6ed

	ldx #CLOSE
	sta $f2f4
	stx $f2f5

	ldx #OPEN
	sta $f593
	stx $f594
	.)

	; -----------------------------
	; init reset code

	.(
	ldx #NEWRESET
	ldy $fd1c
	stx $fd1c
	sty origres
	.)
	
	; -----------------------------
	; install interrupt code
	.(
        ldx #NEWIRQ

        ; x is not modified
        lda $e455       ; standard interrupt routine
        cmp #$4c        ; JMP on some ROMs
        beq dojmp
        ldy #$e456
        bra inject

dojmp   ldy $e456       ; E455 JMP XXXX ; get address from JMP
        iny 
inject
	; set address we actually patch
        sty addr2
        sty addr3

	; protect from interrupts
        php
        sei

	; switch target address of JSR in interrupt routine for ours
	; x has the new interrupt target address
	; $ffff is replaced 
addr2	=*+1
        ldy $ffff	; save the original
addr3	=*+1
        stx $ffff       ; set from above

	; y has the original interrupt routine address
	; we're patching into.
	
        ; save the original addr
        sty origirq

	; -----------------------------
	; exit install

        plp
	rts
	.)
	.)
	
	; -----------------------------
	; patch in that the boot keyboard is
	; always sent to the kernel, and 
	; output is always also sent to the 
	; serial output
ioext_patch4_serkbd_xl .(

	;  F26D		PLA
	;  F26E		JMP $E202	; -	Output to Screen
	;  F271	iF271	BMI $F277
	ldx #NEWSCR
	ldy $f26f
	stx $f26f
	sty SCRRET
	rts
	.)



