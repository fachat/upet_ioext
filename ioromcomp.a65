
.(

#include "ioext-core.i65"

; address of the IO ROM in DOSBANK
IOROM	=$4000

	; -------------------------------------------------------
	; the following code is copied into the ROM and called
	; from the patches into the kernal I/O code

CLRCH

;--------------------------------------------------
; this is injected at $f818 where keyboard is already branched out
;
; F810		LDA $D4		; Current Device Number
; F812		BNE $F818
; ...
; F818	iF818	CMP #$03
; F81A		BEQ $F828
; F81C		BCS $F82D
;
CKOUT	.(
	jsr is_ioext
	bcs chkout_ioext
	cmp #3
	beq chkout_screen
	jmp $f81c
chkout_screen
	jmp $f828	
chkout_ioext
	ldx #IOCOMPBANK
	stx BANK
	jsr IO_CHKOUT
	ldx #0
	stx BANK
	jmp $f82a
	.)

;--------------------------------------------------
; this is injected a $f7ca
;
; F7C6		LDA $D4		; Current Device Number
; F7C8		BEQ $F7DA
; F7CA		CMP #$03
; F7CC		BEQ $F7DA
; F7CE		BCS $F7DF
;
CHKIN	.(
	jsr is_ioext
	bcs chkin_ioext
	cmp #3
	beq chkin_screen
	jmp $f7ce
chkin_screen
	jmp $f7da
chkin_ioext
	ldx #IOCOMPBANK
	stx BANK
	jsr IO_CHKIN
	ldx #0
	stx BANK
	jmp $f7dc
	.)

CHROUT

CHRIN

;--------------------------------------------------
; this is injected at F35D
;
; F356	iF356	LDA $D4		; Current Device Number
; F358		BNE $F35D
; F35A	iF35A	JMP $BF00	; synerr	Output ?SYNTAX Error
; F35D	iF35D	CMP #$03
; F35F		BEQ $F35A
; F361		BCC $F3D4
; F363		LDA #$60
LOAD	.(
	jsr is_ioext
	bcs load_ioext
	cmp #3
	beq load_err
	jmp $f361
load_ioext
load_err
	jmp $f35a		; error - load not supported
	.)

;--------------------------------------------------
; this is injected at $f6ec
;
; F6E3	iF6E3	LDA $D4		; Current Device Number
; F6E5		BNE $F6EC
; F6E7	iF6E7	LDY #$74
; F6E9		JMP $F5AF
; F6EC	iF6EC	CMP #$03
; F6EE		BEQ $F6E7
; F6F0		BCC $F742
SAVE	.(
	jsr is_ioext
	bcs save_ioext
	cmp #3
	beq save_err
	jmp $f6f0
save_ioext
save_err
	jmp $f6e7		; error, save not supported
	.)


;--------------------------------------------------
; close is injected at f4f2, where keyboard and
; screen have already branched out
;
; F2EC	LDA $D4
; F2EE	BEQ $F318
; F2F0	CMP #$03
; F2F2	BEQ $F318
; F2F4	BCS $F315
; F2F6	LDA $D3		; tape
; F2F8	AND #$0f
CLOSE	.(
	jsr is_ioext
	bcs close_ioext
	cmp #3
	bcs closenottape
	lda $D3
	jmp $f2f8
closenottape
	jmp $F315
close_ioext
	ldx #IOCOMPBANK
	stx BANK
	jsr IO_CLOSE
	ldx #0
	stx BANK
	jmp $F318
	.)

;--------------------------------------------------
; open is injected at f593
; where 0 is already escaped to an RTS (keyboard),
; and screen (3) is already branched out as well
; the device number is in AC
;
; F58F	CMP #$03
; F591	BEQ $F5E4	; screen
; F593	BCC $F598	; 1,2 = TAPE
; F595	JMP $F4A5	; IEEE
OPEN	.(
	jsr is_ioext
	bcs open_ioext
	cmp #3
	bcc opentape
	jmp $f598
opentape
	jmp $f4a4
open_ioext
	ldx #IOCOMPBANK
	stx BANK
	jsr IO_OPEN
	ldx #0
	stx BANK
	rts
	.)

;--------------------------------------------------
; filter device number for IOEXT devices
;
; Input:
;	2	Serial1 - replaces Tape2
;	24	(I2C)
;	25	Tape2 (mapped to 2 on original)
;	26	Serial2
;	27	(SPI)
; Output:
;	C=1 ->	is IOEXT, AC is
;	0	(I2C)
;	1	Ser2
;	2	Ser1
;	3	(SPI)
;	C=0 -> original,
is_ioext .(
	cmp #2
	bcc no		; tape1
	beq ser1	; ser1
	cmp #24		; rest up to 23 is IEEE/IEC
	bcc no
	cmp #28		; 4 device nos in ioext
	bcs no
	cmp #25
	beq tape2
ser1	lda #25
yes	sec
	sbc #24		; return AC as 0,1,2,3
	rts
tape2	lda #2
no	clc
	rts
	.)



.)

